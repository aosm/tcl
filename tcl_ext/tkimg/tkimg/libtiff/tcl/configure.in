dnl
dnl Process this file with autoconf to produce a configure script.
dnl
AC_REVISION($Id: configure.in 157 2008-10-23 16:09:37Z nijtmans $)

#--------------------------------------------------------------------
# Configure script for package 'tifftcl', as distributed at November 20, 2001.
# TEA compliant.
#--------------------------------------------------------------------

AC_INIT([tifftcl],[3.8.2])

TEA_INIT([3.7])

AC_CONFIG_AUX_DIR(../../tclconfig)

#--------------------------------------------------------------------
# Load the tclConfig.sh file
#--------------------------------------------------------------------

TEA_PATH_TCLCONFIG
TEA_LOAD_TCLCONFIG

#-----------------------------------------------------------------------
# Handle the --prefix=... option by defaulting to what Tcl gave.
# Must be called after TEA_LOAD_TCLCONFIG and before TEA_SETUP_COMPILER.
#-----------------------------------------------------------------------

TEA_PREFIX

#-----------------------------------------------------------------------
# Standard compiler checks.
# This sets up CC by using the CC env var, or looks for gcc otherwise.
# This also calls AC_PROG_CC, AC_PROG_INSTALL and a few others to create
# the basic setup necessary to compile executables.
#-----------------------------------------------------------------------

TEA_SETUP_COMPILER
AC_HEADER_STDC

# Determine a full path for the chosen compiler to ensure that the
# libtiff configure tool is using the same one as ourselves.

TIFFCC=`echo "$CC" | sed -e 's/-pipe//'`
TIFFCC=`which $TIFFCC 2>/dev/null`

#--------------------------------------------------------------------
# Run the configuration of TIFF itself, and do some special checks
# to if the configuration agress with the sources of tiff itself.
# F.e. tiff is hardwired to use "stdarg.h". If the chosen compiler
# does not support this we have to bail out.
#--------------------------------------------------------------------

if test `grep STDC_HEADERS confdefs.h | wc -l` -eq 0
then
    AC_MSG_ERROR([Compiler does not use stdarg.h, required for TIFF])
fi

mv -f confdefs.h tifftcl_confdefs.h
if test "X$host" = X -o "X$host" = XNONE
then
    TIFFHOST=""
else
    TIFFHOST="$host"
fi
${srcdir}/../configure --disable-zlib --disable-pixarlog --disable-jpeg \
	"--with-CC=$TIFFCC" $TIFFHOST
mv -f tifftcl_confdefs.h confdefs.h

#-----------------------------------------------------------------------
# Specify the C source files to compile in TEA_ADD_SOURCES,
# public headers that need to be installed in TEA_ADD_HEADERS,
# stub library C source files to compile in TEA_ADD_STUB_SOURCES,
# and runtime Tcl library files in TEA_ADD_TCL_SOURCES.
# This defines PKG(_STUB)_SOURCES, PKG(_STUB)_OBJECTS, PKG_HEADERS
# and PKG_TCL_SOURCES.
#-----------------------------------------------------------------------

TEA_ADD_SOURCES([tifftcl.c tifftclStubInit.c])

TEA_ADD_SOURCES([
	../libtiff/tif_aux.c		../libtiff/tif_close.c		../libtiff/tif_codec.c
	../libtiff/tif_compress.c	../libtiff/tif_dir.c		../libtiff/tif_dirinfo.c
	../libtiff/tif_dirread.c	../libtiff/tif_dirwrite.c	../libtiff/tif_dumpmode.c
	../libtiff/tif_error.c		../libtiff/tif_fax3.c
	../libtiff/tif_getimage.c	../libtiff/tif_flush.c		../libtiff/tif_luv.c
	../libtiff/tif_lzw.c		../libtiff/tif_next.c		../libtiff/tif_open.c
	../libtiff/tif_packbits.c	../libtiff/tif_predict.c	../libtiff/tif_print.c
	../libtiff/tif_read.c		../libtiff/tif_swab.c		../libtiff/tif_strip.c
	../libtiff/tif_thunder.c	../libtiff/tif_tile.c		../libtiff/tif_version.c
	../libtiff/tif_warning.c	../libtiff/tif_write.c
	../libtiff/tif_color.c		../libtiff/tif_extension.c
])

TEA_ADD_HEADERS([tifftcl.h tifftclDecls.h])
TEA_ADD_HEADERS([tifftclDeclsMask.h tifftclDeclsUnmask.h])
TEA_ADD_INCLUDES([-I\"`\${CYGPATH} \${srcdir}`\"])
TEA_ADD_INCLUDES([-I\"./libtiff\"])
TEA_ADD_INCLUDES([-I\"`\${CYGPATH} \${srcdir}/../libtiff`\"])
TEA_ADD_STUB_SOURCES([tifftclStubLib.c])
TEA_ADD_SOURCES([tifftclStubLib.c])
TEA_ADD_TCL_SOURCES([])

#--------------------------------------------------------------------
# __CHANGE__
# Choose which headers you need.  Extension authors should try very
# hard to only rely on the Tcl public header files.  Internal headers
# contain private data structures and are subject to change without
# notice.
# This MUST be called after TEA_LOAD_TCLCONFIG / TEA_LOAD_TKCONFIG
#--------------------------------------------------------------------

TEA_PUBLIC_TCL_HEADERS
#TEA_PRIVATE_TCL_HEADERS

#--------------------------------------------------------------------
# A few miscellaneous platform-specific items:
#
# We have to define a special symbol for Windows (BUILD_tifftcl in this
# case) so that we create the export library with the dll.
#
# Windows creates a few extra files that need to be cleaned up.
# We can add more files to clean if our extension creates any extra
# files in the future.
#
# Define any extra compiler flags in the PACKAGE_CFLAGS variable.
# These will be appended to the current set of compiler flags for
# your system.
#--------------------------------------------------------------------

if test "${TEA_PLATFORM}" = "windows" ; then
    AC_DEFINE(BUILD_tifftcl)
    CLEANFILES="*.lib *.dll *.exp *.ilk *.pdb vc*.pch"
    TEA_ADD_SOURCES([../libtiff/tif_win32.c])
else
    CLEANFILES=""
    TEA_ADD_SOURCES([../libtiff/tif_unix.c])
fi
AC_SUBST(CLEANFILES)

#--------------------------------------------------------------------
# Check whether --enable-threads or --disable-threads was given.
# So far only Tcl responds to this one.
#--------------------------------------------------------------------

TEA_ENABLE_THREADS

#--------------------------------------------------------------------
# The statement below defines a collection of symbols related to
# building as a shared library instead of a static library.
#--------------------------------------------------------------------

TEA_ENABLE_SHARED

#--------------------------------------------------------------------
# This macro figures out what flags to use with the compiler/linker
# when building shared/static debug/optimized objects.  This information
# can be taken from the tclConfig.sh file, but this figures it all out.
#--------------------------------------------------------------------

TEA_CONFIG_CFLAGS

#--------------------------------------------------------------------
# Set the default compiler switches based on the --enable-symbols option.
#--------------------------------------------------------------------

TEA_ENABLE_SYMBOLS

#--------------------------------------------------------------------
# Everyone should be linking against the Tcl stub library.  If you
# can't for some reason, remove this definition.  If you aren't using
# stubs, you also need to modify the SHLIB_LD_LIBS setting below to
# link against the non-stubbed Tcl library.
#--------------------------------------------------------------------

AC_DEFINE(USE_TCL_STUBS)

#--------------------------------------------------------------------
# This macro generates a line to use when building a library.  It
# depends on values set by the TEA_ENABLE_SHARED, TEA_ENABLE_SYMBOLS,
# and TEA_LOAD_TCLCONFIG macros above.
#--------------------------------------------------------------------

TEA_MAKE_LIB

#--------------------------------------------------------------------
# __CHANGE__
# Add platform libs to LIBS or SHLIB_LD_LIBS as necessary.
#--------------------------------------------------------------------

if test "${TEA_PLATFORM}" = "windows" -a "$GCC" != "yes" ; then
    TEA_ADD_LIBS([user32.lib])
else
    TEA_ADD_LIBS([-lm])
fi

#--------------------------------------------------------------------
# Find tclsh so that we can run pkg_mkIndex to generate the pkgIndex.tcl
# file during the install process.  Don't run the TCLSH_PROG through
# ${CYGPATH} because it's being used directly by make.
# Require that we use a tclsh shell version 8.2 or later since earlier
# versions have bugs in the pkg_mkIndex routine.
# Add WISH as well if this is a Tk extension.
#--------------------------------------------------------------------

TEA_PROG_TCLSH
TEA_EXPORT_CONFIG(tifftcl)

#--------------------------------------------------------------------
# Finally, substitute all of the various values into the Makefile.
# You may alternatively have a special pkgIndex.tcl.in or other files
# which require substituting th AC variables in.  Include these here.
#--------------------------------------------------------------------

AC_OUTPUT([Makefile tifftclConfig.sh])

#--------------------------------------------------------------------
