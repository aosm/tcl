# $Id: s.adapter.xotcl 1.4 01/03/23 22:24:35+01:00 neumann@somewhere.wu-wien.ac.at $
Class Adapter -superclass Class  

@ @File {
  description {
    Simple adapter pattern meta-class taken from the paper 
    'Filters as a Language Support for Design Patterns in
    Object-Oriented Scripting Languages'. 
  }
}

Adapter instproc adapterFilter args { 
  set r [self calledproc]
  my instvar adaptee specificRequest 
  if {[info exists specificRequest($r)]} {
    return [eval $adaptee $specificRequest($r) $args]
  }
  next
}

Adapter instproc init args {
  my instfilterappend adapterFilter
  next
  my instproc setRequest {r sr} {
    my set specificRequest($r) $sr
  }
  my instproc setAdaptee {a} {
    my set adaptee $a
  }
}
